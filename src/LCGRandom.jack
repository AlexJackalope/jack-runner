class LCGRandom {
    static int seed;
    static int A;
    static int M;
    static int Q;
    static int R;

    function void setSeed(int newSeed) {
        let seed = newSeed;
        if(seed=0) {
           let seed=1;
        }
        let A=219;
        let M=32749;
        let Q=M/A;
        let R=Utils.mod(M,A);
        return;
    }

    /* returns a random int in range 0..(M-1) inclusive */
    function int rand() {
        var int test;
        let test=(A*(Utils.mod(seed,Q)))-(R*(seed/Q));
        if(test<0) {
           let seed=test+M;
        }
        else {
           let seed=test;
        }
        return seed;
    }

    /* returns a random int in range low..high inclusive */
    function int randRange(int low, int high) {
       var int scale;
       let scale = (M / (high - low + 1));
       return (LCGRandom.rand() / scale) + low;
    }

    function void getSeedFromUser() {
      var int newSeed;
      var int addr;
      do Output.moveCursor(10, 22);
		do Output.printString("Wait seed from you");
      do Output.moveCursor(12, 22);
		do Output.printString("Press some button");
      let newSeed = Keyboard.readChar();
      do LCGRandom.setSeed(newSeed);
      
		let addr = 22;
		
		while (addr < 45) {
			do Output.moveCursor(10, addr);
         do Output.moveCursor(12, addr);
			let addr = addr + 1;
		}	
      return;
    }
}