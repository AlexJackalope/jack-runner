class SquareGame {
   field Square square; // the square of this game
   field int direction; // the square's current direction: 
                        // 0=none, 1=up, 2=down, 3=left, 4=right
    field SimpleWall upperWall;
    field SimpleWall lowerWall;
    field int randPos;

   /** Constructs a new square game. */
   constructor SquareGame new() {
      // The initial square is located in (0,0), has size 30, and is not moving.
      let square = Square.new(0, 0, 30);
      do LCGRandom.setSeed(10);
      let direction = 0;
      return this;
   }

   /** Disposes this game. */
   method void dispose() {
      do square.dispose();
      do Memory.deAlloc(this);
      return;
   }

   /** Moves the square in the current direction. */
   method void moveSquare() {
      if (direction = 1) { do square.moveUp(); }
      if (direction = 2) { do square.moveDown(); }
      if (direction = 3) { do square.moveLeft(); }
      if (direction = 4) { do square.moveRight(); }
      return;
   }

    method void moveWall() {
      var boolean wallMoved;
      if (upperWall = null) {
         do createLineOfWalls();
      }
      let wallMoved = upperWall.tryMove() & lowerWall.tryMove();
      if (~(wallMoved))
      {
        do disposeOfWalls();
        do createLineOfWalls();
      }
      do Sys.wait(25);  // по сути скорость игры. Чем меньше, тем игра быстрее
      return;
   }

   method void createLineOfWalls()
   {
      let randPos = LCGRandom.randRange(1, 184);
      let upperWall = SimpleWall.new(500, 0, 10, randPos);
      let lowerWall = SimpleWall.new(500, randPos + 70, 10, (185 - randPos));
      return;
   }

   method void disposeOfWalls()
   {
      do upperWall.erase();
      do upperWall.dispose();
      do lowerWall.erase();
      do lowerWall.dispose();
      return;
   }

   /** Runs the game: handles the user's inputs and moves the square accordingly */
   method void run() {
      var char key;  // the key currently pressed by the user
      var boolean exit, isCollided;
      let exit = false;
      let isCollided = false;
      
      while (~exit) {
         let key = Keyboard.keyPressed();
         do moveWall();
         if (~(isCollided))
         {
            do moveSquare();
            // let direction = 0;
            let isCollided = checkCollision(square, upperWall) | checkCollision(square, lowerWall);
         }
         if (key = 81)  { let exit = true; }     // q key
         if (key = 131) { let direction = 1; }   // up arrow
         if (key = 133) { let direction = 2; }   // down arrow
         // if (key = 130) { let direction = 3; }   // left arrow
         // if (key = 132) { let direction = 4; }   // right arrow
     } 
     return;
   }

   method boolean checkCollision(Square player, SimpleWall someWall) {
    var RectangleCollider playerCollider, wallCollider;
    let playerCollider = player.getCollider();
    let wallCollider = someWall.getCollider();
    return playerCollider.isCollided(wallCollider);
   }
}


